plugins {
    id 'vertex.java-conventions'
    id 'vertex.openapi-generator'
}

description = 'Vertex API Client Library for Java'

dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.14'
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2'
    implementation 'org.apache.commons:commons-lang3:3.15.0'
    implementation 'javax.annotation:javax.annotation-api:1.3'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    
    testImplementation(platform('org.junit:junit-bom:5.10.3'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.3')
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
}

// Override publication configuration for this specific library
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'api-client-java'
            pom {
                name = 'com.vertexvis:api-client-java'
                description = 'The Vertex REST API client for Java.'
            }
        }
    }
}

// Ensure proper task dependencies for generated sources
tasks.named('sourcesJar') {
    dependsOn tasks.openApiGenerate
}

tasks.named('javadocJar') {
    dependsOn tasks.openApiGenerate
}

javadoc {
    options.tags = ["http.response.details:a:Http Response Details"]
    if(JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    dependsOn tasks.openApiGenerate
}
