apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
group = 'com.vertexvis'
version = '0.1.2'

buildscript {
    repositories {
        maven { url "https://repo1.maven.org/maven2" }
        mavenCentral()
    }
}
repositories {
    mavenCentral()
}
sourceSets {
    main.java.srcDirs = ['src/main/java']
}
install {
    repositories.mavenInstaller {
        pom.artifactId = 'api-client-java'
    }
}
javadoc {
    options.tags = ["http.response.details:a:Http Response Details"]
}
task execute(type: JavaExec) {
    main = System.getProperty('mainClass')
    classpath = sourceSets.main.runtimeClasspath
}
task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}
def un = hasProperty('ossrhUsername') ? ossrhUsername : ''
def pw = hasProperty('ossrhPassword') ? ossrhPassword : ''
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: un, password: pw)
            }
            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: un, password: pw)
            }
            pom.project {
                name 'api-client-java'
                artifactId 'api-client-java'
                packaging 'jar'
                description 'The Vertex REST API client for Java.'
                url 'https://github.com/Vertexvis/vertex-api-client-java'
                scm {
                    connection 'scm:git:git@github.com:vertexvis/vertex-api-client-java.git'
                    developerConnection 'scm:git:git@github.com:vertexvis/vertex-api-client-java.git'
                    url 'https://github.com/Vertexvis/vertex-api-client-java'
                }
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/Vertexvis/vertex-api-client-java/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        email 'support@vertexvis.com'
                        name 'Vertex Developers'
                        organizationUrl 'https://developer.vertexvis.com/'
                    }
                }
            }
        }
    }
}
dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.2'
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
